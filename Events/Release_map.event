#include "REDAHelper.event"

EventPointerTable(7, Ch0PointerList)

VeinEffect(0, FreezeAllEnemies)
VeinEffect(1, CreateCliff)
VeinEffect(2, CreateBridges)

Ch0PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(8,TurnBasedEdelSpawn,0,255)
END_MAIN

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
Arrive(4,14)
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
AREA 7 CliffInstigationEvent [15, 5] [22, 12] 
END_MAIN

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

Units1:
UNIT Eirika  EirikaLord 0x1 Level(12,Ally,0)     [24,12] 0b 0x0 1 REDA24R13 [Rapier,Vulnerary] NoAI
UNIT LArachel  Troubadour 0x1 Level(15,Ally,0)  [24,12] 0b 0x0 1 REDA23R14 [Mend,KnightCrest] NoAI
UNIT Tana  PegasusKnight 0x1 Level(15,Ally,0)    [24,12] 0b 0x0 1 REDA26R13 [HeavySpear] NoAI
UNIT Dozla  Berserker 0x1 Level(1,Ally,0) [24,12] 0b 0x0 1 REDA24R14 [BattleAxe,Elixir] NoAI
UNIT

Units2:
UNIT Edelgard  EdelgardLord 0x1 Level(10,Ally,0)   [28,0] 0b 0x0 1 REDA28R1 [Aymr,Elixir] NoAI
UNIT Linhardt  Mage 0x1 Level(11,Ally,0)  [28,0] 0b 0x0 1 REDA27R0 [Elfire,GuidingRing] NoAI
UNIT

Units3:
UNIT Ares  Cavalier 0x1 Level(10,Ally,0) [8,9] 0b 0x0 0x0 0x0 [Mystletainn] NoAI
UNIT Lene  Dancer 0x1 Level(12,Ally,0)    [9,10] 0b 0x0 0x0 0x0 [SleepSword,NinissGrace] NoAI
UNIT

Units4:
UNIT Palla  WyvernKnight 0x1 Level(3,Ally,0)   [19,16] 0b 0x0 0x0 0x0 [Dragonspear] NoAI
UNIT Catria  Falcoknight 0x1 Level(1,Ally,0)     [20,16] 0b 0x0 0x0 0x0 [KillerLance] NoAI
UNIT

Units5:
UNIT Yuri  Rogue 0x1 Level(2,Ally,0)  [3,0] 0b 0x0 0x0 0x0 [IronSword] NoAI
UNIT

EnemyBosses:
UNIT Gharnef  Druid 0x1 Level(5,Enemy,0)   [16,7] 0b 0x0 0x0 0x0 [Imhullu] NeverMoveAI
UNIT Nergal  Druid 0x1 Level(5,Enemy,0)    [1,5] 0b 0x0 0x0 0x0 [Ereshkigal] NeverMoveAI
UNIT Riev  Bishop 0x1 Level(5,Enemy,0) [9,4] 0b 0x0 0x0 0x0 [Ivaldi] NeverMoveAI
UNIT Takumi  Sniper 0x1 Level(5,Enemy,0)  [4,14] 0b 0x0 0x0 0x0 [FujinYumi] NeverMoveAI
UNIT

EnemyUnits1:
UNIT Dolhr Fighter Gharnef Level(12,Enemy,1) [19,5] 0 0 1 REDA17R6 [SteelAxe] AttackInRangeAI
UNIT Dolhr Cavalier_F Gharnef Level(12,Enemy,1) [19,5] 0 0 1 REDA15R8 [IronLance] AttackInRangeAI
UNIT Dolhr Mage Gharnef Level(12,Enemy,1) [19,5] 0 0 1 REDA16R9 [Thunder] AttackInRangeAI
UNIT Dolhr Shaman Gharnef Level(12,Enemy,1) [19,5] 2 0 1 REDA18R7 [Flux,Elixir] AttackInRangeAI
UNIT Dolhr Knight Gharnef Level(12,Enemy,1) [19,5] 0 0 1 REDA14R7 [SteelLance] NeverMoveAI
UNIT


Traps1:
//Vein(9,23,0x0,DV1Desc) //freeze enemies
Vein(6,15,1,DV2Desc) //landslide
Vein(14,2,2,DV3Desc) //bridges
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
LOAD1 0x0 Units1 
ENUN
LOAD1 0x0 EnemyBosses
ENUN
LOAD1 0x0 EnemyUnits1
NoFade
ENDA

EndingScene:
ASMC 0x85C71 0x381F5 0x37CF9 //show records screen
STAL 120
EVBIT_F 0x2
MNTS 0x1 //return to title screen
NoFade
ENDA



FreezeAllEnemies:
VeinActivation
ASMC FreezeAllASMC|1
NoFade
ENDA

CreateCliff:
CAM1 [17,8]
VeinActivation
TILECHANGE 0
NoFade
ENDA

CliffInstigationEvent:
CHECK_ALLEGIANCE -1
BNE 1 0 0xC

CAM1 Takumi
CUMO Takumi
STAL 40
CURE
Text(PlaceholderText)
MOVE 0 Takumi [6,15]
ENUN
STAL 10
CALL CreateCliff
Text(PlaceholderText)
GOTO 2
LABEL 1
ENUF 7
LABEL 2
NoFade
ENDA

CreateBridges:
CAM1 [10,5]
VeinActivation
TILECHANGE 1
NoFade
ENDA


TurnBasedEdelSpawn:
CHECK_EVENTID 7
BEQ 1 0 0xC

//check counter
COUNTER_CHECK 0 
SVAL 1 2
BGE 2 0xC 1 

//if less than 2 add 1
COUNTER_INC 0 
GOTO 1

LABEL 2
// spawn edelgard & linhardt w/ text
CAM1 [28,0]
LOAD1 0 Units2
ENUN
CUMO Edelgard
STAL 40
CURE
Text(PlaceholderText)
CAM1 Eirika
CUMO Eirika
STAL 40
CURE
Text(PlaceholderText)
CAM1 Edelgard
CUMO Edelgard
STAL 40
CURE
Text(PlaceholderText)
GOTO 3

LABEL 1
//unset flag 8
ENUF 8

LABEL 3
NoFade
ENDA


